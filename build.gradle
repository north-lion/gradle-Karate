// Ref: https://github.com/karatelabs/karate-examples/tree/main/grpc

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.18'
}

// updating the version in our release process.
def grpcVersion = '1.48.1'
def protobufVersion = '3.21.1'
def protocVersion = protobufVersion

group 'org.example'
version '1.0-SNAPSHOT'
sourceCompatibility = 11
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'com.intuit.karate:karate-junit5:1.4.0'
    // Dependencies To Generate grpc-java Code
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    // Dependencies To aws-java-sdk
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.517')
    implementation 'com.amazonaws:aws-java-sdk-lambda'
}


protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}

test {
    useJUnitPlatform()
}